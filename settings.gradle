plugins {
    id "com.gradle.enterprise" version "3.16.1"
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.12.1'
}

gradleEnterprise {
    server = 'https://ge.grails.org'
    buildScan {
        publishAlwaysIf(System.getenv('CI') == 'true')
        publishIfAuthenticated()
        uploadInBackground = System.getenv("CI") == null
        capture {
            taskInputFiles = true
        }
    }

}

buildCache {
    local { enabled = System.getenv('CI') != 'true' }
    remote(HttpBuildCache) {
        push = System.getenv('CI') == 'true'
        enabled = true
        url = 'https://ge.grails.org/cache/'
        credentials {
            username = System.getenv('GRADLE_ENTERPRISE_BUILD_CACHE_NODE_USER')
            password = System.getenv('GRADLE_ENTERPRISE_BUILD_CACHE_NODE_KEY')
        }
    }}

include 'docs'
include 'plugin'
include 'testapp-spring-security-cas-test1'

project(':testapp-spring-security-cas-test1').projectDir = new File(settingsDir, 'testapps/testapp-spring-security-cas-test1')
include 'testapp-spring-security-cas-test2'

project(':testapp-spring-security-cas-test2').projectDir = new File(settingsDir, 'testapps/testapp-spring-security-cas-test2')

rootProject.name = 'grails-spring-security-cas'

findProject(':plugin').name = 'spring-security-cas'
